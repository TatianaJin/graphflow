group 'ca.waterloo.dsg'
version '0.1'

apply plugin: 'java'
apply plugin: 'antlr'
apply plugin: 'com.google.protobuf'

repositories {
    mavenCentral()
}

dependencies {
    // ANTLR4
    antlr "org.antlr:antlr4:4.5"
    // gRPC
    compile 'io.grpc:grpc-netty:1.0.1'
    compile 'io.grpc:grpc-protobuf:1.0.1'
    compile 'io.grpc:grpc-stub:1.0.1'
    // log4j
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    // jUnit
    testCompile group: 'junit', name: 'junit', version: '4.+'
    //google
    compile 'com.google.code.gson:gson:2.7'
}

// ANTLR4 configuration.
generateGrammarSource {
    maxHeapSize = "64m"
    arguments += [
        "-long-messages",                                   // show exception details
        "-visitor",                                         // generate parse tree visitor classes
        "-package", "ca.waterloo.dsg.graphflow.grammar"     // specify package for generated classes
    ]
}

// gRPC configuration.
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
    }
}
protobuf {
    protoc {
        // The version of protoc must match protobuf-java.
        artifact = "com.google.protobuf:protoc:3.0.2"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$buildDir/generated-src/proto"
}

// Add the source files generated by ANTLR4 and gRPC to the gradle sourcesets.
sourceSets.main.java.srcDirs += "$buildDir/generated-src/antlr"
sourceSets.main.java.srcDirs += "$buildDir/generated-src/proto"

// Enable additional compile time warnings.
compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

apply plugin: 'application'
// Disable the default assembly tasks.
startScripts.enabled = false
run.enabled = false
distZip.enabled = false
distTar.enabled = false
// Create the convenience executables.
def scripts = [
    'graphflow-server'  : 'ca.waterloo.dsg.graphflow.server.GraphflowServerRunner',
    'graphflow-cli'     : 'ca.waterloo.dsg.graphflow.cli.GraphflowCliRunner'
]
scripts.each() { fileName, className ->
    tasks.create(name: fileName, type: CreateStartScripts) {
        mainClassName = className
        applicationName = fileName
        outputDir = new File(project.buildDir, 'scripts')
        classpath = jar.outputs.files + project.configurations.runtime
    }
    applicationDistribution.into("bin") {
        from(tasks[fileName])
        fileMode = 0755
    }
}

// Define specific gradle version.
task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
